bundle common redmine_g { 
  vars:
    any::
      "url" string => "http://rubyforge.org/frs/download.php/76718/redmine-1.4.7.tar.gz";
      "target" string => "/tmp/redmine-1.4.7.tar.gz";

    redmine_akronet::
      "conf[akronet][adapter]" string => "postgresql";
      "conf[akronet][host]" string => "localhost";
      "conf[akronet][database]" string => "redmine_akronet_production";
      "conf[akronet][username]" string => "akronet";
      "conf[akronet][password]" string => "$(private_g.password[postgres_redmine_akronet])";
      "conf[akronet][root]" string => "/srv/akronet.synth.pl";
      "conf[akronet][plugin]" slist => { "redmine_akronet_mods" };
      "conf[akronet][port]" string => "3000";

    redmine_internal::
      "conf[internal][adapter]" string => "postgresql";
      "conf[internal][host]" string => "localhost";
      "conf[internal][database]" string => "redmine_internal_production";
      "conf[internal][username]" string => "issues";
      "conf[internal][password]" string => "P@ssw0rd";
      "conf[internal][root]" string => "/srv/issues.synth.pl";
      "conf[internal][plugin]" slist => { cf_null };
      "conf[internal][port]" string => "3100";

    any::
      "plugin[redmine_akronet_mods][url]" string => "git@bitbucket.org:kklimonda/redmine_akronet_mods.git";
} 

bundle agent redmine
{
  packages:
      "rails"
        package_policy => "addupdate",
        package_method => gem_fuzzy_versioned,
        package_version => "2.3.16";
      "bundler"
        package_policy => "add",
        package_method => gem;  
      "coderay"
        package_policy => "add",
        package_method => gem;
      "fastercsv"
        package_policy => "add",
        package_method => gem;
      "i18n"
        package_policy => "add",
        package_method => gem_versioned,
        package_version => "0.4.2";
      "libpq-dev"
        package_policy => "addupdate",
        package_method => generic;    
      "pg"
        package_policy => "add",
        package_method => gem;
      "tzinfo"
        package_policy => "addupdate",
        package_method => gem_fuzzy_versioned,
        package_version => "0.3.31";
      # retarded requirement, some bug in Redmine most likely as removing ldap in bundler doesn't help
      "net-ldap"
        package_policy => "add",
        package_method => gem;

  vars:
    any::
      "instance" slist => getindices("redmine_g.conf");

  methods:
      "fix_gem_perms" usebundle => ruby_fix_gems_perms;
      "redmine_tarball_is_present" usebundle => redmine_tarball_is_present;
      "redmine_tarball_is_unpacked" usebundle => redmine_tarball_is_unpacked("$(instance)");
      "redmine_create_dabase" usebundle => redmine_create_database("$(instance)");
      "redmine_instance_configured" usebundle => redmine_instance_configured("$(instance)");
      "redmine_install_plugins" usebundle => redmine_install_plugins("$(instance)");
      "redmine_configure_unicorn" usebundle => redmine_configure_unicorn("$(instance)");
      "redmine_configure_nginx" usebundle => redmine_configure_nginx("$(instance)");
}

bundle agent redmine_tarball_is_present
{
  classes:
      "redmine_tarball_is_present" expression => fileexists("$(redmine_g.target)");

  commands:
    !redmine_tarball_is_present::
      "/usr/bin/wget -q -O $(redmine_g.target) $(redmine_g.url)";
}

bundle agent redmine_tarball_is_unpacked(instance)
{
  files:
    "$(redmine_g.conf[$(instance)][root])/."
        perms => mog("0644", "www-data", "www-data"),
        depth_search => recurse_include_base("inf"),
        action => immediate;
    "$(redmine_g.conf[$(instance)][root])/."
        perms => mog("0755", "www-data", "www-data"),
        file_select => dirs,
        depth_search => recurse_include_base("inf"),
        action => immediate;

  classes:
      "redmine_tarball_is_unpacked" expression => fileexists("$(redmine_g.conf[$(instance)][root])/Rakefile");

  commands:
    !redmine_tarball_is_unpacked::
      "/bin/tar"
        args => "zxf $(redmine_g.target) -C $(redmine_g.conf[$(instance)][root])/ --strip-components 1";
}

bundle agent redmine_create_database(instance)
{
  vars:
      "dbuser" string => "$(redmine_g.conf[$(instance)][username])";
      "dbpasswd" string => "$(redmine_g.conf[$(instance)][password])";
      "dbname" string => "$(redmine_g.conf[$(instance)][database])";

  databases:
      "$(redmine_g.conf[$(instance)][database])"
        database_operation => "create",
        database_type => "sql",
        database_server => postgres_master("$(dbuser)", "$(dbpasswd)");

  classes:
      "db_user_exists"
        comment => "Check if the Database user already exists",
        expression => returnszero("/bin/echo \"SELECT 1 FROM pg_roles WHERE rolname='$(dbuser)'\" |/usr/bin/sudo -u postgres psql -tA | /bin/grep -q 1", "useshell");
      "password_changed"
        comment => "Check if the password should be updated",
        not => returnszero("/bin/echo \"SELECT 1;\"| /usr/bin/env PGPASSWORD=\"$(dbpasswd)\" psql -U $(dbuser) $(dbname) > /dev/null 2>&1", "useshell");

  commands:
    !db_user_exists::
      "/usr/bin/createuser $(dbuser) --no-superuser --createdb --no-createrole"
        comment => "Create the Database user for the application",
        contain => setuid("postgres");
    password_changed::
      "/bin/echo \"ALTER USER $(dbuser) with encrypted password '$(dbpasswd)'\" | /usr/bin/psql template1"
        comment => "Set password for the PostgreSQL user $(dbuser)",
        contain => setuid_sh("postgres");
}

bundle agent redmine_instance_configured(instance)
{
  vars:
      "root" string => "$(redmine_g.conf[$(instance)][root])";

  files:
      "$(root)/Gemfile"
        edit_line => replace_or_add("^gem \"rails\", \"2.3.15\"", "gem \"rails\", \"~> 2.3.16\""),
        classes => if_repaired("gemfile_updated");
      "$(root)/lib/tasks/absolute_migrate.rake"
        copy_from => remote_cp("$(sys.workdir)/masterfiles/templates/redmine/absolute_migrate.rake", "$(sys.policy_hub)");
      "$(root)/config/database.yml"
        comment => "All the database settings go into database.yml file.",
        create => "true",
        edit_line => redmine_edit_database_yml("$(instance)");
      "$(root)/config/configuration.yml"
        comment => "The rest of the settings are in the configuration.yml file.",
        copy_from => remote_cp("$(sys.workdir)/masterfiles/templates/redmine/configuration.yml", "$(sys.policy_hub)");

  classes:
      "redmine_configured"
        comment => "Redmine is considered to be configured when both Bundler and Rake's generate_session_store has been run",
        and => {
          fileexists("$(root)/.bundle/config"),
          fileexists("$(root)/config/initializers/session_store.rb")
        };

  commands:
    !redmine_configured||gemfile_updated::
      "/usr/local/bin/bundle" args => "install --without sqlite mysql test development rmagick openid",
        comment => "Run Bundler once to configure it",
        contain => in_dir("$(root)");
    redmine_configured&gemfile_updated::
      "/usr/sbin/service" args => "unicorn_akronet restart";
    !redmine_configured::
      "/usr/local/bin/rake" args => "generate_session_store",
        comment => "Run the generate_session_store command to generate the secret session key for Rails",
        contain => in_dir("$(root)");
      "/usr/local/bin/rake RAILS_ENV=production" args => "db:migrate",
        comment => "Create the database structure, but only after database have already been created",
        contain => in_dir("$(root)");
}

bundle edit_line redmine_edit_database_yml(instance)
{
  vars:
      "conf" slist => { "@(redmine_g.conf[$(instance)])" };

  insert_lines:
      "production:";
      "  adapter: postgresql";
      "  database: $(redmine_g.conf[$(instance)][database])";
      "  host: $(redmine_g.conf[$(instance)][host])";
      "  username: $(redmine_g.conf[$(instance)][username])";
      "  password: $(redmine_g.conf[$(instance)][password])";
      "  encoding: utf8";

  delete_lines:
      ".*";
}

bundle agent redmine_install_plugins(instance)
{
  methods:
    "$(instance)_plugins" usebundle => redmine_install_plugins_expand("$(instance)", "@(redmine_g.conf[$(instance)][plugin])");
}

# A workaround for a stupid limitation of CFEngine3 where it doesn't expand lists that are embedded inside arrays
bundle agent redmine_install_plugins_expand(instance, plugins)
{
  methods:
    "redmine_install_plugin" usebundle => redmine_install_plugin("$(instance)", "$(plugins)");
} 

bundle agent redmine_install_plugin(instance, plugin_name)
{
  vars:
    "root" string => "$(redmine_g.conf[$(instance)][root])";

  classes:
    "plugin_installed" expression => fileexists("$(root)/vendor/plugins/$(plugin_name)/.");

  commands:
    !plugin_installed::
      "/usr/bin/git clone $(redmine_g.plugin[$(plugin_name)][url])",
        contain => in_dir("$(root)/vendor/plugins/");
      "/usr/local/bin/rake RAILS_ENV=production" args => "db:migrate_plugins",
        contain => in_dir("$(root)");
}

bundle agent redmine_configure_unicorn(instance)
{
  packages:
      "unicorn"
        package_method => gem;

  vars:
      "port" string => "$(redmine_g.conf[$(instance)][port])";
      "root" string => "$(redmine_g.conf[$(instance)][root])";
      "pid_file" string => "$(root)/unicorn.pid";
      "log_file" string => "$(root)/log/unicorn.log";

  processes:
      ".*unicorn_rails.*$(instance)\.ru.*"
        restart_class => "restart_unicorn";

  files:
      "/usr/local/etc/unicorn/."
        create => "true";
      "/tmp/unicorn.ru.tmpl"
        copy_from => remote_cp("$(sys.workdir)/masterfiles/templates/unicorn.ru.tmpl", "$(sys.policy_hub)");
      "/tmp/unicorn.upstart.tmpl"
        copy_from => remote_cp("$(sys.workdir)/masterfiles/templates/unicorn.upstart.tmpl", "$(sys.policy_hub)");
      "/usr/local/etc/unicorn/$(instance).ru"
        create => "true",
        edit_defaults => empty,
        edit_line => expand_template("/tmp/unicorn.ru.tmpl"),
        classes => if_repaired("restart_unicorn");
      "/etc/init/unicorn_$(instance).conf"
        create => "true",
        edit_defaults => empty,
        edit_line => expand_template("/tmp/unicorn.upstart.tmpl"),
        classes => if_repaired("restart_unicorn");

  methods:
    "fix_gem_perms" usebundle => ruby_fix_gems_perms,
      handle => "fix_gem_perms";

  commands:
    restart_unicorn.!no_restarts::
        "/usr/sbin/service unicorn_$(instance) restart",
          depends_on => { "fix_gem_perms" };
}

bundle agent redmine_configure_nginx(instance)
{
  vars:
      "name" string => "$(instance)";
      "root" string => "$(redmine_g.conf[$(instance)][root])";
      "fastcgi_pass" string => "unix:$(root)/dispatch.sock";
      "server_name" string => "$(instance).$(def.domain)";
      "upstream_servers" string => "127.0.0.1:$(redmine_g.conf[$(instance)][port])";

  files:
      "/tmp/nginx_site.tmpl"
        copy_from => remote_cp("$(sys.workdir)/masterfiles/templates/redmine_site.tmpl", "$(sys.policy_hub)");
      "/etc/nginx/sites-available/$(instance)"
        create => "true",
        edit_defaults => empty,
        edit_line => expand_template("/tmp/nginx_site.tmpl"),
        classes => if_repaired("reload_nginx");
      "/etc/nginx/sites-enabled/$(instance)"
        link_from => ln_s("/etc/nginx/sites-available/$(instance)"),
        classes => if_repaired("reload_nginx");

  commands:
    reload_nginx::
      "/usr/sbin/service nginx reload";
}
