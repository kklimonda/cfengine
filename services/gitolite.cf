bundle agent gitolite
{
  vars:
      "url" string => "git://github.com/sitaramc/gitolite";

  files:
      "/tmp/admin.pub"
        depends_on => { "user_exists" },
        handle => "admin_key",
        perms => mo("600", "git"),
        copy_from => remote_cp("$(sys.workdir)/masterfiles/templates/gitolite.sshkey", "$(sys.policy_hub)");
      "/var/gitolite/bin/."
        handle => "bin_dir",
        depends_on => { "user_exists" },
        perms => mo("755", "git"),
        create => "true";

  classes:
      "gitolite_cloned"
         comment => "Check if the gitolite repository has been cloned already",
         expression => fileexists("/var/gitolite/source/.");
      "user_exists"
         comment => "Check if the git user exists",
         expression => userexists("git");

  commands:
    !user_exists::
      "/usr/sbin/useradd" args => "--home-dir /var/gitolite/ --create-home git",
        handle => "user_exists",
        comment => "Create user owning gitolite repository";
    !gitolite_cloned::
      "/usr/bin/git" args => "clone $(url) /var/gitolite/source/",
        contain => setuid("git");
    any::
      "/var/gitolite/source/install" args => "-ln /var/gitolite/bin/",
        depends_on => { "bin_dir" },
        contain => setuid("git");
      "/usr/bin/env HOME=/var/gitolite /var/gitolite/bin/gitolite setup --pubkey /tmp/admin.pub",
        depends_on => { "admin_key" },
        contain => setuid_sh("git");
}
